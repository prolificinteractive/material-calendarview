apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'com.github.dcendents.android-maven'

android {
  compileSdkVersion 28

  defaultConfig {
    minSdkVersion 16
    targetSdkVersion 27

    versionCode Integer.parseInt(project.VERSION_CODE)
    versionName project.VERSION_NAME

    vectorDrawables.useSupportLibrary = true
  }

  lintOptions {
    abortOnError false
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
    buildTypes {
        staging {
        }
    }
}

group = "com.github.prolificinteractive"
version = android.defaultConfig.versionName
archivesBaseName = 'material-calendarview'

dependencies {
  implementation 'com.jakewharton.threetenabp:threetenabp:1.1.1'
  implementation 'androidx.appcompat:appcompat:1.0.0'
  implementation 'androidx.annotation:annotation:1.0.0'

  // SSP / SDP
  implementation 'com.intuit.ssp:ssp-android:1.0.6'
  implementation 'com.intuit.sdp:sdp-android:1.0.6'
}

tasks.withType(Javadoc) {
  options.addStringOption('Xdoclint:none', '-quiet')
  options.addStringOption('encoding', 'UTF-8')
  options.addStringOption('charSet', 'UTF-8')
}

android.libraryVariants.all { variant ->
  task("generate${variant.name.capitalize()}Javadoc", type: Javadoc) {
    title "Material CalendarView ${version}"
    description "Generates Javadoc for $variant.name."
    source = variant.javaCompiler.source
    doFirst {
      classpath = files(variant.javaCompiler.classpath.files, project.android.getBootClasspath())
    }
    exclude '**/BuildConfig.java'
    exclude '**/R.java'
    options {
      links "http://docs.oracle.com/javase/7/docs/api/"
      linksOffline "http://d.android.com/reference", "${android.sdkDirectory}/docs/reference"
    }
  }
}